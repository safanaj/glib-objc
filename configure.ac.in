dnl configure.ac
dnl
dnl glib-objc - objective-c bindings for glib
dnl
dnl Copyright (c) 2008 Brian Tarricone <bjt23@cornell.edu>
dnl

dnl version info
m4_define([glib_objc_verinfo], [0:0:0])
m4_define([glib_objc_api_version], [2.0])
m4_define([go_version_major], [0])
m4_define([go_version_minor], [1])
m4_define([go_version_micro], [0])
m4_define([go_version_build], [@REVISION@])
m4_define([go_version_tag], [git])
m4_define([go_version], [go_version_major().go_version_minor().go_version_micro()ifelse(go_version_tag(), [git], [go_version_tag()-go_version_build()], [go_version_tag()])])

dnl init autoconf
AC_INIT([glib-objc], [go_version], [bjt23@cornell.edu])
AC_PREREQ([2.59])

dnl init automake
AM_INIT_AUTOMAKE([dist-bzip2])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

dnl check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX

dnl check for basic programs
AC_PROG_CC
AC_PROG_OBJC
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_LIBTOOL

dnl check for standard header files
AC_HEADER_STDC

dnl version information
GO_VERSION=go_version
GLIB_OBJC_VERINFO=glib_objc_verinfo
GLIB_OBJC_API_VERSION=glib_objc_api_version
AC_SUBST(GO_VERSION)
AC_SUBST(GLIB_OBJC_VERINFO)
AC_SUBST(GLIB_OBJC_API_VERSION)

dnl required

PKG_CHECK_MODULES([GLIB], [glib-2.0 >= 2.12.0], , [AC_MSG_ERROR([
*** Glib version 2.12.0 or greater is required.
*** YOu can download it from http://gtk.org/
])])
AC_MSG_CHECKING([GLIB_CFLAGS])
AC_MSG_RESULT([$GLIB_CFLAGS])
AC_MSG_CHECKING([GLIB_LIBS])
AC_MSG_RESULT([$GLIB_LIBS])

PKG_CHECK_MODULES([GOBJECT], [gobject-2.0 >= 2.12.0], , [AC_MSG_ERROR([
*** GObject (a part of the Glib library) version 2.12.0 or greater is required.
*** You can download it from http://gtk.org/
])])
AC_MSG_CHECKING([GOBJECT_CFLAGS])
AC_MSG_RESULT([$GOBJECT_CFLAGS])
AC_MSG_CHECKING([GOBJECT_LIBS])
AC_MSG_RESULT([$GOBJECT_LIBS])

dnl check for debugging support
AC_ARG_ENABLE([debug],
              [AC_HELP_STRING([--enable-debug=[full|yes|no]],
                              [Build with debugging support])
               AC_HELP_STRING([--disable-debug],
                              [Include no debugging support (default)])],
              [], [enable_debug=no])
AC_MSG_CHECKING([whether to build with debugging support])
if test "x$enable_debug" != "xno"; then
    AC_DEFINE([DEBUG], [1], [Define if debugging support is enabled])
    if test "x$GCC" = "xyes"; then
        if test "x$enable_debug" = "xfull"; then
            DEBUG_CFLAGS="-Wall -Wno-deprecated-declarations -g3 -Werror -fno-strict-aliasing"
        else
            DEBUG_CFLAGS="-Wall -g"
        fi
    fi
    DEBUG_CFLAGS="$DEBUG_CFLAGS -DG_DISABLE_DEPRECATED"
    
    CFLAGS="$CFLAGS $DEBUG_CFLAGS"
    OBJCFLAGS="$OBJCFLAGS $DEBUG_CFLAGS"
    CXXFLAGS="$CXXFLAGS $DEBUG_CFLAGS"
fi
AC_MSG_RESULT([$enable_debug])

AC_OUTPUT([
Makefile
common/Makefile
glib-objc/Makefile
glib-objc/glib-objc-2.0.pc
gobject-objc/Makefile
gobject-objc/gobject-objc-2.0.pc
tests/Makefile
])
